#!/usr/bin/zsh
cache_file="/tmp/current-monitor"
current_mode=0

if [ ! -e "$cache_file" ]; then
    echo "$current_mode" > "$cache_file"
else
    current_mode=$(< "$cache_file")
fi

allDisplays=($(xrandr | grep " connected " | awk '{ print $1 }'))
laptopDisplay=${allDisplays[1]}
externalDisplays=("${allDisplays[@]/$laptopDisplay}")

# Set mode manually
if [[ "$1" != "" ]]; then
    current_mode=$1
fi

case $current_mode in
    0) # Laptop screen only
        current_mode_name="laptop"
        xrandr --output $laptopDisplay --auto
        for display in ${externalDisplays[@]}
        do
            xrandr --output $display --off
        done
    ;;
    1) # Only external screen
        current_mode_name="external"
        xrandr --output $laptopDisplay --off
        for display in ${externalDisplays[@]}
        do
            xrandr --output $display --auto
        done
    ;;
    2) # Mirror laptop to external
        current_mode_name="mirror"
        xrandr --output $laptopDisplay --auto
        for display in ${externalDisplays[@]}
        do
            xrandr --output $display --auto --output $display --same-as $laptopDisplay
        done
    ;;
    *)
        echo "We should not be here!"
        exit 1
    ;;
esac

if type "notify-send.sh" > /dev/null; then
    notify-send.sh --replace-file=/tmp/current-monitor-notification "Display output changed" "Mode: $current_mode_name"
fi

if [[ "$1" == "" ]]; then
    current_mode=$((current_mode + 1))

    if [ $current_mode -gt 2 ]; then
        current_mode=0
    fi

    echo "$current_mode" > "$cache_file"
fi

